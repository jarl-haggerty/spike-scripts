var output%, Input%, InputChannel%, WorkChannel%, Regions$[50], RegionStarts[50], RegionStops[50], RegionCount%, swap$[50], dataswap%, original%, Threshold%, ReservedCursors%;

View(App(3)).WindowVisible(0);
View(LogHandle()).Window(70,0,100,100);
View(LogHandle()).WindowVisible(1);

ReservedCursors% := 3;
Input% := -1;
SetupCSV();
ToolbarClear();
ToolbarSet(0, "Idle", Idle%);
ToolbarSet(1, "&Quit", Quit%);
ToolbarSet(2, "&Open", Open%);
ToolbarSet(3, "&Parse Bursts", Parse%);
ToolbarSet(4, "&Add Region", AddRegion%);
ToolbarSet(5, "&Remove Region", RemoveRegion%);
ToolbarSet(6, "&Save", Save%);
Toolbar("Burst Parser", 152);

View(App(3)).WindowVisible(1);

Func SetupCSV()
	ReadSetup(",\t", " ", "\"", "\"", ",");
end;

Func Idle%()
	return 1;
end;

Func Quit%()
	if input% > 0 then
		View(input%);
		ChanDelete(WorkChannel%);
		FileClose();
	endif;
	return 0;
end;

Func Parse%()
	View(Input%);
	DataSwap% := FileOpen(Print$("%s.swap", FileName$(3)), 8, 2);
	View(Input%);

	var NewPosition, Maxtime$, Button%, PeakPosition, LastBurst, ViewWidth;
	Maxtime$ := Print$("%f", View(Input%).Maxtime());
	View(Input%).CursorVisible(2, 1);
	
	LastBurst := -1;
	Button% := 3;
	while Button% > 2 do
		View(Input%).CursorActive(2, 0);
		View(Input%).CursorActive(1, 7, WorkChannel%, "Cursor(1)", Maxtime$, "Cursor(1)", View(Input%).HCursor(Threshold%));
		NewPosition := View(Input%).CursorSearch(1);
		if NewPosition > -1 then
			View(Input%).Cursor(2, NewPosition);
			View(Input%).CursorActive(1, 0);
			View(Input%).CursorActive(2, 8, WorkChannel%, "Cursor(2)", Maxtime$, "Cursor(2)", 0.0);
			View(Input%).CursorSearch(1);
			View(Input%).CursorActive(2, 0);
			View(Input%).CursorActive(1, 4, WorkChannel%, "Cursor(1)", "Cursor(2)", "Cursor(1)");
			PeakPosition := View(Input%).CursorSearch(1);
			View(Input%).CursorActive(2, 0);
			View(Input%).CursorActive(1, 8, WorkChannel%, "Cursor(1)", "0", "Cursor(1)", 0.0);
			View(Input%).CursorSearch(1, 1);
			ViewWidth := View(Input%).XHigh() - View(Input%).XLow();
			View(Input%).XRange(PeakPosition - ViewWidth/8, PeakPosition - 7*ViewWidth/8);
			Button% := Interact("Record Burst?", 152, 0, "Stop", "Record and Stop", "Record and continue", "Don't record and continue");
			if Button% = 2 or Button% = 3 then
				if LastBurst >= 0 then
					View(DataSwap%).Print("%f, %f, %f, %f, %f, %f\n", View(Input%).Cursor(1), View(Input%).ChanMeasure(WorkChannel%, 1, View(Input%).Cursor(1), View(Input%).Cursor(2)), 
																									              View(Input%).ChanMeasure(WorkChannel%, 10, View(Input%).Cursor(1), View(Input%).Cursor(2)),
																									              View(Input%).Cursor(2)-View(Input%).Cursor(1),
																									              (PeakPosition-View(Input%).Cursor(1))/(View(Input%).Cursor(2)-View(Input%).Cursor(1)),
																									              1/(View(Input%).Cursor(1)-LastBurst));
				else
					View(DataSwap%).Print("%f, %f, %f, %f, %f, -1\n", View(Input%).Cursor(1), View(Input%).ChanMeasure(WorkChannel%, 1, View(Input%).Cursor(1), View(Input%).Cursor(2)), 
																									              View(Input%).ChanMeasure(WorkChannel%, 10, View(Input%).Cursor(1), View(Input%).Cursor(2)),
																									              View(Input%).Cursor(2)-View(Input%).Cursor(1),
																									              (PeakPosition-View(Input%).Cursor(1))/(View(Input%).Cursor(2)-View(Input%).Cursor(1)));
				endif;
				LastBurst := View(Input%).Cursor(1);
			endif;
			View(Input%).Cursor(1, View(Input%).Cursor(2));
		else
			Message("No burst found");
			return 1;
		endif;
	wend;
	View(Input%).CursorVisible(2, 0);
	View(DataSwap%);
	FileClose(DataSwap%);
	return 1;
end;

Func Save%()
	View(Input%);
	var InputName$, i%;
	InputName$ := FileName$(3);

	Output% := FileOpen(Print$("%s.csv", InputName$), 8, 1);
	DataSwap% := FileOpen(Print$("%s.swap", InputName$), 8, 0);

	View(Output%).Print("Label, Start, Stop\n");
	for i% := 0 to RegionCount%-1 do
		View(Output%).Print("%s, %f, %f\n", Regions$[i%], Min(View(Input%).Cursor(ReservedCursors% + 2*i%), View(Input%).Cursor(ReservedCursors% + 2*i% + 1)), 
		                                                  Max(View(Input%).Cursor(ReservedCursors% + 2*i%), View(Input%).Cursor(ReservedCursors% + 2*i% + 1)))
	next;

	View(Output%).Print("Time, Area, Amplitude, Duration, Fractional Peak, Frequency\n");
	while View(DataSwap%).Read(swap$[0:10]) = 6 do
		View(Output%).Print("%s, %s, %s, %s, %s, %s\n", swap$[0], swap$[1], swap$[2], swap$[3], swap$[4], swap$[5]);
	wend;

	View(Output%);
	FileClose(Output%);
	View(DataSwap%);
	FileClose(DataSwap%);

	return 1;
end;

Func AddRegion%()
	Regions$[RegionCount%] := Input$("Input the name of the new region:", "");
	View(Input%);
	CursorNew(XLow() + (XHigh() - XLow())/4, ReservedCursors% + 2*RegionCount%);
	CursorActive(ReservedCursors% + 2*RegionCount%, 0);
	CursorLabel(4, ReservedCursors% + 2*RegionCount%, Regions$[RegionCount%]);
	CursorNew(XLow() + 3*(XHigh() - XLow())/4, ReservedCursors% + 2*RegionCount% + 1);
	CursorActive(ReservedCursors% + 2*RegionCount% + 1, 0);
	CursorLabel(4, ReservedCursors% + 2*RegionCount% + 1, Regions$[RegionCount%]);
	RegionCount% := RegionCount% + 1;
	return 1;
end;

Func RemoveRegion%()
	if RegionCount% > 0 then
		var index%, result%, v%;
		DlgCreate("Remove Region");
		DlgList(0, "Select a region to remove:", Regions$[]);
		result% := DlgShow(index%);
		
		if result% = 1 then
			View(input%);
			for v% := index% to RegionCount%-2 do
				Regions$[v%] := Regions$[v%+1];
				Cursor(ReservedCursors% + 2*v%, Cursor(ReservedCursors% + 2*(v%+1)));
				CursorLabel(4, ReservedCursors% + 2*v%, Regions$[v%]);
				Cursor(ReservedCursors% + 2*v% + 1, Cursor(ReservedCursors% + 2*(v%+1) + 1));
				CursorLabel(4, ReservedCursors% + 2*v% + 1, Regions$[v%]);
			next;
			v% := v% + 1;
			CursorDelete(ReservedCursors% + 2*v%);
			CursorDelete(ReservedCursors% + 2*(v%+1));
			RegionCount% := RegionCount% - 1;
		endif;
	else
		Message("There are no regions to remove");
	endif;
	return 1;
end;

Func Open%()
	Input% := FileOpen("*.smr", 0);
	CursorDelete(-1);
	var outputname$;
	outputname$ := Print$("%s.csv", FileName$(3));
	output% := FileOpen(outputname$, 8, 0);
	outputname$ := Print$("%s.swap", FileName$(3));
	dataswap% := FileOpen(outputname$, 8, 1);
	RegionCount% := 0;
	if output% > 0 then
		View(output%).Read();
		while View(output%).Read(swap$[0:10]) = 3 do
			Regions$[RegionCount%] := swap$[0];
			View(input%).CursorNew(Val(swap$[1]), ReservedCursors% + 2*RegionCount%);
			View(input%).CursorActive(ReservedCursors% + 2*RegionCount%, 0);
			View(input%).CursorLabel(4, ReservedCursors% + 2*RegionCount%, Regions$[RegionCount%]);
			View(input%).CursorNew(Val(swap$[2]), ReservedCursors% + 2*RegionCount% + 1);
			View(input%).CursorActive(ReservedCursors% + 2*RegionCount% + 1, 0);
			View(input%).CursorLabel(4, ReservedCursors% + 2*RegionCount% + 1, Regions$[RegionCount%]);
			RegionCount% := RegionCount% + 1;
		wend;
		while View(output%).Read(swap$[0:10]) = 6 do
			View(dataswap%).Print("%s, %s, %s, %s, %s, %s\n", swap$[0], swap$[1], swap$[2], swap$[3], swap$[4], swap$[5]);
		wend;
		View(output%);
		FileClose(output%);
	endif;
	View(DataSwap%);
	FileClose(DataSwap%);

	View(input%);
	Window(0, 0, 70, 100);
	CursorNew(XLow() + (XHigh() - XLow())/4, 1);
	CursorLabel(4, 1, "Start");
	CursorNew(0, 2);
	CursorLabel(4, 2, "Stop");
	CursorVisible(2, 0);
	WindowVisible(1);

	InputChannel% := Input("Which channel are you working on?", 1);

	ChanHide(-1);
	ChanShow(InputChannel%);
	
	WorkChannel% := ChanDuplicate(InputChannel%);
	ChanProcessAdd(WorkChannel%, 8, .05);
	ChanProcessAdd(WorkChannel%, 2, 5);
	ChanShow(WorkChannel%);
	Optimise();

	HCursorDelete(-1);
	Threshold% := HCursorNew(WorkChannel%, 0);
	HCursorLabel(4, Threshold%, "Threshold");
	return 1;
end;
